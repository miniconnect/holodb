plugins {
    id 'java-library'
    id 'application'
    id 'eclipse'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

description "HoloDB standalone app"

ext.mainClass = 'hu.webarticum.holodb.app.launch.HolodbServerMain'

sourceSets {
    lab {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    labImplementation.extendsFrom implementation
    labRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation project(':core')
    implementation project(':storage')
    implementation project(':values')
    implementation "hu.webarticum.miniconnect:messenger:${miniConnectVersion}"
    implementation "hu.webarticum.miniconnect:rdbms-framework:${miniConnectVersion}"
    implementation "hu.webarticum.miniconnect:record:${miniConnectVersion}"
    implementation "hu.webarticum.miniconnect:server:${miniConnectVersion}"
    implementation "hu.webarticum.miniconnect:util:${miniConnectVersion}"
    
    implementation 'hu.webarticum.strex:strex:1.2.0'
    implementation 'com.github.mifmif:generex:1.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.7.1'
    testImplementation 'org.assertj:assertj-core:3.19.0'
}

application {
    mainClass = project.ext.mainClass
}

jib {
    to {
        image = 'miniconnect/holodb'
        tags = [version, 'latest']
        auth {
            username = findProperty('dockerHubUsername') ?: ''
            password = findProperty('dockerHubPassword') ?: ''
        }
    }
    container {
        ports = ['3430']
        args = ['/app/config.yaml']
        mainClass = project.ext.mainClass
    }
}

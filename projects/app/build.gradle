plugins {
    id 'java-library'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

description = "HoloDB standalone app"

ext.mainClass = 'hu.webarticum.holodb.app.launch.HolodbServerMain'

sourceSets {
    lab {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    labImplementation.extendsFrom implementation
    labRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation project(':bootstrap')
    
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.25.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.1'
}

application {
    mainClass = project.ext.mainClass
}

jib {
    to {
        image = 'miniconnect/holodb'
        tags = [version, 'latest']
        auth {
            username = findProperty('dockerHubUsername') ?: ''
            password = findProperty('dockerHubPassword') ?: ''
        }
    }
    container {
        ports = ['3430']
        args = ['/app/config.yaml']
        mainClass = project.ext.mainClass
    }
}

ext.graalDockerName = 'miniconnect/holodb-graal'
ext.graalDockerPlatforms = [

    // these are supported by the normal native-image-edition docker (while the musl version is not multi-platform):
    'linux/amd64',
    'linux/arm64',
    
    /*
    'linux/arm64/v8',
    'linux/arm/v7',
    'linux/386',
    'linux/ppc64le',
    'linux/s390x',*/
]

tasks.register('graalDockerBuild', Exec) {
    dependsOn(tasks.named('shadowJar'))
    workingDir = project.projectDir
    commandLine(
        'docker', 'build',
        '-t', "${graalDockerName}:latest",
        '-f', 'Dockerfile.graal',
        '.'
    )
}

tasks.register('graalDockerBuildMultiplatform', Exec) {
    dependsOn(tasks.named('shadowJar'))
    workingDir = project.projectDir
    commandLine(
        'docker', 'buildx', 'build',
        '--platform', graalDockerPlatforms.join(','),
        '-t', "${graalDockerName}:latest",
        '-f', 'Dockerfile.graal',
        '.',
    )
}

tasks.register('graalDockerExtractBinaries') {
    inputs.property('versionStr', project.version ?: 'SNAPSHOT')
    inputs.property('outputPath', "${buildDir}/native-image/bin")
    inputs.property('dockerName', graalDockerName)
    inputs.property('dockerPlatforms', graalDockerPlatforms)
    doLast {
        def versionStr = inputs.properties['versionStr']
        def outputDir = file(inputs.properties['outputPath'])
        def dockerName = inputs.properties['dockerName']
        def dockerPlatforms = inputs.properties['dockerPlatforms']
        outputDir.mkdirs()
        dockerPlatforms.each { platformName ->
            def platformTag = platformName.replaceAll('/', '_')
            def imageName = "${dockerName}:latest-${platformTag}"
            def outputFileName = "holodb-${versionStr}-${platformTag}"
            def outputPath = new File(outputDir, outputFileName).absolutePath
            println "Extract native executable for platform '${platformName}' to ${outputPath}"
            def containerIdOutput = new ByteArrayOutputStream()
            exec {
                commandLine(
                    'docker', 'buildx', 'build',
                    '--platform', platformName,
                    '-t', "${dockerName}:latest-${platformTag}",
                    '--load',
                    '-f', 'Dockerfile.graal',
                    '.',
                )
            }
            exec {
                commandLine 'docker', 'create', '--platform', platformName, imageName
                standardOutput = containerIdOutput
            }
            def containerId = containerIdOutput.toString().trim()
            if (containerId.isEmpty()) {
                throw new GradleException("Failed to create container for platform: ${platformName}")
            }
            exec { commandLine 'docker', 'cp', "${containerId}:/app/app", outputPath }
            exec { commandLine 'docker', 'rm', containerId }
            exec { commandLine 'docker', 'image', 'rm', imageName }
        }
    }
}

plugins {
    id 'java-library'
    id 'me.champeau.gradle.jmh' version '0.5.0'
    id 'io.morethan.jmhreport' version '0.9.0'
}

description "Core building blocks"

sourceSets {
    lab {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    labImplementation.extendsFrom implementation
    labRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation "hu.webarticum.miniconnect.api:lang:${miniConnectApiVersion}"
    implementation "hu.webarticum.miniconnect:util:${miniConnectVersion}"
    implementation "hu.webarticum.minibase:engine:${miniBaseVersion}"
    
    implementation 'org.apache.commons:commons-lang3:3.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    
    labImplementation 'jfree:jfreechart:1.0.13'
    
    jmh 'org.openjdk.jmh:jmh-core:1.34'
    jmhRuntimeOnly 'org.openjdk.jmh:jmh-generator-annprocess:1.34'
}

jmhJar {
    duplicatesStrategy = DuplicatesStrategy.WARN
}

jmh {
    duplicateClassesStrategy = 'exclude'
    resultFormat = 'JSON'
}

tasks.getByName('jmhJar').doFirst() {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

jmhReport {
    jmhResultPath = project.file('build/reports/jmh/results.json')
    jmhReportOutput = project.file('build/reports/jmh/html')
}

tasks.getByName('jmhReport').doFirst() {
    project.file('build/reports/jmh/html').mkdir()
}

